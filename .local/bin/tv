#!/usr/bin/env zsh

# Functions for session management
function switch_to() {
	if [[ -z $TMUX ]]; then
		tmux attach-session -t "$1"
	else
		tmux switch-client -t "$1"
	fi
}

function has_session() {
	tmux has-session -t "$1" 2>/dev/null
}

function hydrate() {
	if [[ -f "$2/.tmux-sessionizer" ]]; then
		tmux send-keys -t "$1" "source $2/.tmux-sessionizer" C-m
	fi
}

# Collect projects
projects=()
base_dirs=(${HOME}/Templates/*/*(/))

function contains_only_dirs() {
	local dir=$1
	[[ -n "$(ls -A $dir)" ]] && [[ -z "$(find $dir -maxdepth 1 -type f)" ]]
}

for dir in $base_dirs; do
	if contains_only_dirs "$dir"; then
		projects+=("$dir"/*(/:A))
	else
		projects+=($dir)
	fi
done

display_projects=("${projects[@]#$HOME/Templates/}")
selected_display="$(print -lnr "${display_projects[@]}" | fzf)"
selected=""
[[ "$selected_display" ]] && selected="${projects[${display_projects[(i)$selected_display]}]}"

# Exit if no selection was made
if [[ -z "${selected}" ]]; then
	exit 0
fi

# Create session name and change directory
session_name=$(basename "${selected}" | tr -c '[:alnum:]' '_')
cd "${selected}"

# Create and configure the session
if ! has_session "$session_name"; then
	tmux new-session -d -s "$session_name" -n "editor" -c "${selected}" "nvim ."
	tmux new-window -t "$session_name:2" -n "shell" -c "${selected}"
	tmux new-window -t "$session_name:3" -n "git" -c "${selected}" "nvim -c MagitOnly"
	tmux select-window -t "$session_name:1"
	hydrate "$session_name" "${selected}"
fi

# Attach to session
switch_to "$session_name"
